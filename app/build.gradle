/*
 * MIT License
 *
 * Copyright (c) 2022 Ahmed Tikiwa
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify, merge, publish, distribute,
 * sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING
 * BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

plugins {
    id("com.android.application")
    id("androidx.navigation.safeargs")
    id("com.google.gms.google-services")
    id("com.google.firebase.crashlytics")
    id("com.google.firebase.firebase-perf")
    id("dagger.hilt.android.plugin")
    id("kotlin-android")
    id("kotlin-kapt")
    id 'com.google.devtools.ksp' version "$kotlin_version-1.0.9"
    id("kotlin-parcelize")
    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_version"
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.theupnextapp"
        minSdkVersion 22
        targetSdkVersion 33
        versionCode 183
        versionName "3.5.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField('String', 'TRAKT_CLIENT_ID', localProperties.getProperty("TraktClientID"))
        it.buildConfigField('String', 'TRAKT_CLIENT_SECRET', localProperties.get("TraktClientSecret"))
        it.buildConfigField('String', 'TRAKT_REDIRECT_URI', localProperties.getProperty("TraktRedirectURI"))
    }

    buildFeatures {
        dataBinding true
        buildConfig true
        compose true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    namespace 'com.theupnextapp'

    ksp {
        arg("compose-destinations.generateNavGraphs", "true")
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.google.android.material:material:$material_version"

    // Compose
    // Integration with activities
    implementation "androidx.activity:activity-compose:$compose_activity_version"
    // Compose Material Design
    implementation "androidx.compose.material3:material3:$compose_material_version"
    implementation "androidx.compose.material3:material3-window-size-class:$compose_material_version"
    // Animations
    implementation "androidx.compose.animation:animation:$compose_animation_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    // Integration with ViewModels
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$compose_viewmodel_version"
    // Compose Coil
    implementation "io.coil-kt:coil-compose:$compose_coil_version"
    // Hilt Navigation
    implementation "androidx.hilt:hilt-navigation-compose:$compose_hilt_navigation_version"
    // Compose Livedata runtime
    implementation "androidx.compose.runtime:runtime-livedata:$compose_ui_version"
    // Compose Icons Extended
    implementation "androidx.compose.material:material-icons-extended:$compose_material_icons_extended_version"

    // Coroutines for getting off the UI thread
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Retrofit for networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofit_coroutines_adapter_version"

    // Moshi for parsing the JSON format
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // OkHTTP for Retrofit
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_interceptor_version"

    // Glide for images
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // ViewModel and LiveData (arch components)
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Data store
    implementation "androidx.datastore:datastore-preferences:$datastore_version"

    // Room database
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Hilt Work
    implementation "androidx.hilt:hilt-work:$hilt_work_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Timber
    // Logging
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Navigation
    implementation "androidx.hilt:hilt-navigation-fragment:$hilt_navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Gson
    implementation "com.squareup.retrofit2:converter-gson:$gson_version"

    // Jsoup
    implementation "org.jsoup:jsoup:$jsoup_version"

    implementation "com.google.firebase:firebase-core:$firebase_core_version"

    // Firebase Analytics (Kotlin)
    implementation "com.google.firebase:firebase-analytics-ktx:$firebase_analytics_version"

    // Crashlytics
    implementation "com.google.firebase:firebase-crashlytics:$firebase_crashlytics_version"

    // Recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    // For control over item selection of both touch and mouse driven selection
    implementation "androidx.recyclerview:recyclerview-selection:$recyclerview_selection_version"

    // Performance Monitoring
    implementation "com.google.firebase:firebase-perf:$firebase_performance_monitoring_version"

    // PreferenceManager
    implementation "androidx.preference:preference-ktx:$androidx_preference_version"

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Chrome Custom Tabs
    implementation "androidx.browser:browser:$custom_tabs_version"

    // Flexbox
    implementation "com.google.android.flexbox:flexbox:$flexbox_version"

    // Jetpack Compose Destinations
    implementation "io.github.raamcosta.compose-destinations:core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    // Accompanist Navigation Animation
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_navigation_animation_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_test_version"
}
